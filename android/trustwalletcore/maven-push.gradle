apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.jfrog.artifactory'

version project.property('version')
group 'com.trustwallet'

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/trustwalletcore-release.aar")
            artifact sourcesJar { 
                classifier "sources"
            }
            groupId this.group
            artifactId 'wallet-core'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

artifactory {
    contextUrl = 'https://oss.jfrog.org'
    resolve {
        repository {
            repoKey = 'libs-release'
        }
    }
    publish {
        repository {
            if (this.version.endsWith("-SNAPSHOT")) {
              repoKey = 'oss-snapshot-local'
            } else {
              repoKey = 'oss-release-local' 
            }
            username = System.getenv('BINTRAY_USER')
            password = System.getenv('BINTRAY_KEY')
        }
        defaults {
            publications 'Production'
            properties = [ 'bintray.repo': 'trust/wallet-core', 'bintray.package': 'com.trustwallet.wallet-core', 'bintray.version': this.version.toString() ]
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        userOrg = 'trust'
        repo = 'wallet-core'
        name = 'com.trustwallet.wallet-core'
        description = "Cross-platform, cross-blockchain wallet library."
        publish = true
        publicDownloadNumbers = true
        licenses = ['MIT']
        vcsUrl = 'https://github.com/trustwallet/wallet-core.git'
        dryRun = false
        version {
            name = this.version
            desc = "Wallet Core ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}